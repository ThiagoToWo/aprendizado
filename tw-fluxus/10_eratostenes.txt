#CRIVO DE ERATOSTENES
# Implementa o Crivo de Eratostenes em um vetor
# para identificar os primos de 2 a 1000 e
# imprime eles em linhas de amplitude 100.

{
	<< "Insira o limite de teste: ";
	>> m;
	<< "Insira a amplitude de distribuicao: ";
	>> a;

	p[0] = 0;
	p[1] = 0;

	i = 2;
	1; ?i > m? -> 2;
		p[i] = 1;
		i = i + 1;
		-> 1;
	2;

	i = 2;
	3; ?i * i > m? -> 4;
		j = 2 * i;
		5; ?j > m? -> 6;
			p[j] = 0;
			j = j + i;
			-> 5;
		6;
		i = i + 1;
		-> 3;
	4;

	i = 2;
	7; ?i > m? -> 8;
		?p[i] != 1? -> 9;		
			<< i, " ";
		9;

		?i % a != 0? -> 10;
			<< "\n";
		10;

		i = i + 1;
		-> 7;
	8;
}